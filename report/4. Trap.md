# Trap

## 概要

当 CPU 访问无效的寄存器地址、进行除零操作或进行 **系统调用** 时，会产生中断。由 **系统调用** 产生的中断称为 **trap** 。为了能够对中断进行一些简单的处理，本章我们需要：

1. 创建 **栈帧** 结构体。

2. 建立 **中断描述符表（interrupt descriptor table）** 。

3. 能够保存和恢复寄存器。


## 创建栈帧结构体

当产生中断时，我们需要保存当前 **所有寄存器** 的状态，然后处理中断，最后恢复寄存器状态，继续执行之前的命令。我们需要按照特定的格式保存寄存器，以便于我们使用 **栈帧** 结构体查看或修改这些寄存器。可以理解为，在一片连续的内存空间中存放了我们寄存器的状态，我们通过这片空间的首地址（指针）来访问他们。在创建结构体之前，我们在 **Cargo.toml** 中需要引入一些依赖：
```
[dependencies]
riscv = { git = "https://github.com/riscv-and-rust-and-decaf/riscv" }
```

**riscv32** 中有 32 个普通寄存器和部分特殊寄存器。在 main.rs 的同级目录下创建 **context.rs** 文件，在开头引入一些特殊寄存器：
```
use riscv::register::{
    sstatus::Sstatus as Xstatus,
    mcause::Mcause,
};
```

栈帧结构体的实现如下：
```
pub struct TrapFrame {
    pub x: [usize; 32], // general registers
    pub sstatus: Xstatus, // Supervisor Status Register
    pub sepc: usize, // Supervisor exception program counter, save the trap virtual address (here is used to save the process program entry addr?)
    pub stval: usize, // Supervisor trap value
    pub scause: Mcause, // scause register: record the cause of exception/interrupt/trap
}
```

理解并创建栈帧之后，我们便可以开始对中断进行处理了。

## 建立中断描述符表（idt）

在 main.rs 的同级目录下创建 **trap/trap.asm** 和 **interrupt.rs** 用于处理中断。

当我们的程序遇上中断（执行到汇编命令 `ecall` 或 `ebreak`）时，会先寻找 **idt** 。因此我们要指定我们使用的 **idt** 。在 **interrupt.rs** 中引入 **栈帧** 和 **stvec** 。产生中断时， **pc寄存器** 会被设置为 **stvec** 。为我们实现了 **指定中断处理函数** 的函数：
```
use crate::context::TrapFrame;
use riscv::register::stvec;

#[no_mangle]
pub fn init() {
    extern {
        fn __alltraps();
    }
    unsafe {
        stvec::write(__alltraps as usize, stvec::TrapMode::Direct);
    }
}
```

**__alltraps** 便是我们的程序在遇上中断时，第一个调用的函数。现在我们来实现他：
```
# in trap.asm

.section .text
.globl __alltraps
__alltraps:
    SAVE_ALL
    mv a0, sp
    jal rust_trap
.globl __trapret
__trapret:
    RESTORE_ALL
    # return from supervisor call
    XRET
```

**.globl** 表示该函数是一个全局函数，可以被外部调用。 **.sction .text** 将 **__alltraps** 和 **__trapret** 放到同一个代码段，这样做的目的我们稍后介绍。

**SAVE_ALL** 保存了所有的寄存器的状态。为了增加代码的可读性，我们使用了较多的宏。在 **main.rs** 中引入 **trap.asm** 之前，我们需要先定义使用的宏：
```
global_asm!("
    .equ xstatus,   0x100
    .equ xscratch,  0x140
    .equ xepc,      0x141
    .equ xcause,    0x142
    .equ xtval,     0x143
    .macro XRET\n sret\n .endm
    .macro TEST_BACK_TO_KERNEL
        andi s0, s1, 1 << 8     // sstatus.SPP = 1
    .endm
");
global_asm!(r"
    .equ XLENB,     4
    .equ XLENb,     32
    .macro LOAD a1, a2
        lw \a1, \a2*XLENB(sp)
    .endm
    .macro STORE a1, a2
        sw \a1, \a2*XLENB(sp)
    .endm
");
global_asm!(include_str!("trap/trap.asm"));
```
> 注意这里 `global_asm!(include_str!("trap/trap.asm"))` 必须放在宏定义之后，否则会导致 trap/trap.asm 中的宏未定义

有了上面定义的宏之后，我们就可以开始编写 **SAVE_ALL** 和 **RESTORE_ALL** 了。增加了这两个部分之后， **trap.asm** 应该长这样：
```
# Constants / Macros defined in Rust code:
#   xscratch
#   xstatus
#   xepc
#   xcause
#   xtval
#   XRET
#   XLENB
#   LOAD
#   STORE
#   TEST_BACK_TO_KERNEL

.macro SAVE_ALL
    # If coming from userspace, preserve the user stack pointer and load
    # the kernel stack pointer. If we came from the kernel, sscratch
    # will contain 0, and we should continue on the current stack.
    csrrw sp, (xscratch), sp
    bnez sp, _save_context
_restore_kernel_sp:
    csrr sp, (xscratch)
    # sscratch = previous-sp, sp = kernel-sp
_save_context:
    # provide room for trap frame
    addi sp, sp, -36 * XLENB
    # save x registers except x2 (sp)
    STORE x1, 1
    STORE x3, 3
    # tp(x4) = hartid. DON'T change.
    # STORE x4, 4
    STORE x5, 5
    STORE x6, 6
    STORE x7, 7
    STORE x8, 8
    STORE x9, 9
    STORE x10, 10
    STORE x11, 11
    STORE x12, 12
    STORE x13, 13
    STORE x14, 14
    STORE x15, 15
    STORE x16, 16
    STORE x17, 17
    STORE x18, 18
    STORE x19, 19
    STORE x20, 20
    STORE x21, 21
    STORE x22, 22
    STORE x23, 23
    STORE x24, 24
    STORE x25, 25
    STORE x26, 26
    STORE x27, 27
    STORE x28, 28
    STORE x29, 29
    STORE x30, 30
    STORE x31, 31

    # get sp, sstatus, sepc, stval, scause
    # set sscratch = 0
    csrrw s0, (xscratch), x0
    csrr s1, (xstatus)
    csrr s2, (xepc)
    csrr s3, (xtval)
    csrr s4, (xcause)
    # store sp, sstatus, sepc, sbadvaddr, scause
    STORE s0, 2
    STORE s1, 32
    STORE s2, 33
    STORE s3, 34
    STORE s4, 35
.endm

.macro RESTORE_ALL
    LOAD s1, 32             # s1 = sstatus
    LOAD s2, 33             # s2 = sepc
    TEST_BACK_TO_KERNEL
    bnez s0, _restore_context   # s0 = back to kernel?
_save_kernel_sp:
    addi s0, sp, 36*XLENB
    csrw (xscratch), s0         # sscratch = kernel-sp
_restore_context:
    # restore sstatus, sepc
    csrw (xstatus), s1
    csrw (xepc), s2

    # restore x registers except x2 (sp)
    LOAD x1, 1
    LOAD x3, 3
    # LOAD x4, 4
    LOAD x5, 5
    LOAD x6, 6
    LOAD x7, 7
    LOAD x8, 8
    LOAD x9, 9
    LOAD x10, 10
    LOAD x11, 11
    LOAD x12, 12
    LOAD x13, 13
    LOAD x14, 14
    LOAD x15, 15
    LOAD x16, 16
    LOAD x17, 17
    LOAD x18, 18
    LOAD x19, 19
    LOAD x20, 20
    LOAD x21, 21
    LOAD x22, 22
    LOAD x23, 23
    LOAD x24, 24
    LOAD x25, 25
    LOAD x26, 26
    LOAD x27, 27
    LOAD x28, 28
    LOAD x29, 29
    LOAD x30, 30
    LOAD x31, 31
    # restore sp last
    LOAD x2, 2
.endm

.section .text
.globl __alltraps
__alltraps:
    SAVE_ALL
    mv a0, sp
    jal rust_trap
.globl __trapret
__trapret:
    RESTORE_ALL
    # return from supervisor call
    XRET
```
> **SAVE_ALL** 和 **RESTORE_ALL** 的实现细节在上面代码的注释中给出，感兴趣的同学可以自行阅读

**a0** 是 riscv32 中的参数寄存器，用于存放下一个调用的函数的参数，这里调用的的函数是 **rust_trap** ：
```
// in interrupt.rs

#[no_mangle]
pub extern "C" fn rust_trap(tf: &mut TrapFrame) {
    println!("here a trap");
    tf.increase_sepc();
}
```

在 riscv32 中，发生中断指令的 **pc** 被存入 **sepc** 。中断处理完成后，需要手动将 **sepc** 的位置往后移动 32 bits （**未加优化** 时，riscv32 中每条指令的大小均为 32 bits），从而执行下一条指令。这里我们通过 **increase_sepc** 完成这个功能。实现起来十分简单，具体如下：
```
// in context.rs

impl TrapFrame {
    pub fn increase_sepc(self: &mut Self) {
        self.sepc = self.sepc + 4;
    }
}
```

**注意！！！**

这里我们强调了 **未加优化** 。对于常用指令，编译器会进行压缩，减小程序的大小。但是有时候这并不是我们希望的。比如这里因为我们要求每条指令都是精准的 **32bits** ，才能够通过 **self.sepc = self.sepc + 4** 跳转至下一条指令（否则会跳转到奇怪的地方）。在 **riscv_32-xy_os.json** 中，有一行 `"features": "+m,+a,+c"` 。默认情况下，riscv32 只支持加减法， **+m** 增加了乘除法； **+a** 增加了原子操作； **+c** 增加了代码优化。这里的优化是我们不想要的，所以把 **+c** 删去。

至此我们简易的的中断功能已经全部实现完成，让我们设置一个中断试试。首先在 **main.rs** 中增加 `#![feature(asm)]` ，然后修改 **rust_main** 为：
```
#[no_mangle]
pub extern "C" fn rust_main() -> ! {
    interrupt::init();
    unsafe{
        asm!("ebreak\n"::::);
    }
    panic!("End of rust_main");
}
```

编译运行，屏幕显示：
```
here a trap
panicked at 'End of rust_main', src/main.rs:51:5
```

可以看到，我们已经成功进入中断处理函数，并且返回到了 **rust_main** ，触发了 panic 。